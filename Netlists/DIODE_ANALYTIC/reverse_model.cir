Test the model in reverse region
***************************************************************
* Tier: ?????
* Directory/Circuit name:  DIODE_ANALYTIC/reverse_model.cir
* Description:  Runs a transient analysis to check the operation of the level
*               1 diode in the reverse active region.
* Input:   No exogenous input; nonzero intial condition specified for V(1).
* Output:  V(1) (capacitor voltage)
*
* Created by:  K. R. Santarelli (08/07) 
*
* We form a circuit that is the series combination of a diode and a capacitor.
* One node of the capacitor is connected to ground, the anode of the diode is
* connected to ground, and the cathode of the diode is connected to the other
* node of the capacitor.  We specify a nonzero initial voltage across the 
* (1V) which places a negative voltage drop (-1V) across the diode, 
* initially.  If no breakdown voltage is specified, the diode initially 
* operates according to its reverse active model, with I-V relationship
*
* ID= -IS * IRF * [1 + (3*N*Vth/(VD*e))^3]   for VD < -3*N*Vth
*
* where IS is the saturation current (default 1e-14, 1 here), IRF is a reverse 
* fitting factor (default 1), N is the emission coefficient (default 1), 
* Vth=k*T/q \approx 25mV at room temperature, and e=exp(1).  If we denote the
* voltage across the capacitor as Vout, then, while we can't solve explicitly
* for Vout as a function of t, we *can* solve for t as a function of Vout:
*
*t(Vout) = -C/IS*[Vout - V0 + a/3 *ln(abs((Vout-a)/(V0-a)))
*	       	       	    - a/6 *ln(abs((Vout^2+a*Vout+a^2)/(V0^2+a*V0+a^2)))
*                           - a/sqrt(3) * arctan(2*Vout/a/sqrt(3)+1/sqrt(3))
*			    + a/sqrt(3) * arctan (2*V0/a/sqrt(3)+1/sqrt(3) ]
*
* where a=3*N*Vth/e and V0 is the initial voltage across the capacitor, which,
* in this case, is 1.  The above expression holds until Vout=3*N*Vth.  
* Plugging in 3*N*Vth into the above expression yields an expression for the
* exact time at which the model ceases to be valid.  Numerical calculation 
* shows that this time is roughly given by 9.25s.
*
* NOTE:  Even though there is technically no exogenous input in this circuit,
*        xyce_verify will complain/crash if there isn't a single DC source 
*	 present.  The voltage source Vdummy has been added to an artificial 
*	 node (node 2) to keep the xyce_verify gods happy.
*
*********************************************************

Vdummy 2 0 1

D1 0 1 Dmod
C1 1 0 10 IC=1


.model Dmod D LEVEL=1 IS=1
.tran 1m 9.25s
.print tran V(1)
.options TIMEINT reltol=1e-6 abstol-1e-6
.end

