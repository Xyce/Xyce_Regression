$subcircuit instance parameters
* Netlist tests XDM subcircuit translations.
* XDM should translate the HSPICE "." node delineation
* into the Xyce ":" form in the output variables as
* well as in the .IC statement. In addition, wildcards
* in subcircuit delineations ought to be removed. 
* Finally, XDM ought to be able to distinguish between
* subckt instance parameters and parameters inside
* subckts. See issues #66, #116 and #136 on XDM 
* gitlab.

.subckt subckt_resistor a b resistance1=1 resistance2=1
.param
+ VAL1 = 2
+ VAL2 = 3
R1 a a1 resistance1
R2 a1 a2 resistance2
R3 a2 b VAL1+VAL2
.ends

.subckt subckt_rc a b resistance=1 capacitance=10p
.param VAL1=10
R1 a a1 resistance
R2 a1 b VAL1
C1 b 0 capacitance
.ends

.param VAL3  = 10
VA 1 0 PULSE(0 1 0 1e-3 1e-3 5e-3 1s)
X1 1 2 subckt_resistor resistance1=val3 resistance2=5
X2 2 3 subckt_rc resistance=10 capacitance=1u
C2 3 0 10u

.IC V(X2.B)=0.5
.TRAN  10u 10ms start=0 UIC
.PRINT TRAN V(X1.*) V(X2.A1) V(X2.B) V(3)
