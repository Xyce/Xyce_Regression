*Testing Gummel-Poon BJT Model
*******************************************************************
* Tier: ?????
* Directory/Circuit name:  BJT_ANALYTIC/ramp_test1.cir
* Description:  Runs a transient analysis to check operation of level 1 BJT
*               model.
* Input:  V1=V(1)
* Output: I(V1) (voltage source current = BJT emitter current)
*
* Created by:  K. R. Santarelli 10/07
*
* NOTES:  We perform a transient simulation to validate the Gummel-Poon BJT 
*         model by connecting the base and collector of an NPN BJT to a ramp
*         voltage (V1(t) = t), connecting the emitter of the BJT to ground, and
*         measuring the corresponding emitter current (we actually measure the
*         current flowing out of the voltage source V1 which is equal to the 
*         emitter current by KCL; measuring the emitter current via IE(Q1) 
*         directly is often-times erroneous in transient simulations and is a
*         separate issue under investigation).  The emitter current of the 
*         transistor is comprised of three main components:
*             
*         1.  Forward/reverse bias diode currents (Icc, Ibe, Ire).
*         2.  A current formed by the time derivative of the total base 
*             charge (i.e., terms of the form dQb/dt).
*         3.  Currents due to depletion capacitance (i.e., terms of the form
*             Cde * dVbe/dt).
*           
*         Note that, compared to the more standard Ebers-Moll model, there 
*         exists a dynamic term (Qbo/Qb) which multiplies Is in the expression
*         for the collector current, as well.  
*         
*         For this particular test, we zero out charge derivative terms by 
*         leaving the forward and reverse transit times at 0.  For the values
*         of the parameters chosen in the model statement below, we find that
*         an analytical expression for the emitter current IE(t) is given by
*
*         IE(t)= IS*( a(t) + IS/BF + ISE) * ( exp(t/Vth) - 1) + b(t) 
*
*         where 
* 
*         a(t) = 1/(0.5 + sqrt(0.25 + IS/IKF * (exp(t/Vth) - 1)))
*
*         b(t) = CJE * (1 - t/VJ) ^ (-M),  for t <= FC*VJ
*         b(t) = CJE * (1 - FC) ^ (-M-1) * [1 - FC*(1+M) + M/VJ * t], 
*                                          for t > FC*VJ
*
*         where BF=100, FC=0.5, VJ=0.75, M=.33, and all other parameters are
*         as given in the model statement below.  We set NE = 1 so that all 
*         exponential terms involved are of the form exp(t/NE*Vth) = 
*         exp(t/NF*Vth) = exp(t/Vth) to simplify the analytical expression.
* 
***************************************************************************

V1 1 0 DC 0 PWL (0 0 1 1) 
Q1 1 1 0 Qmod 
.model Qmod NPN level=1 ISE=1e-12 IS=1e-10 NE=1 IKF=1 CJE=10n
.tran 1u .8
.print tran V(1) I(V1)
.options device gmin=0 voltlim=0
*.options timeint abstol=1e-12 reltol=1e-6 
.options timeint method=7 erroption=1
.end
