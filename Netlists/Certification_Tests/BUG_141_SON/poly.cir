Polynomial Source Netlist
***********************************************************************
* Tier No.:     1
* Description:  Test of the polyinomial functionality for 
*	describing a nonlinear dependent voltage source.  
* Input:  DC Voltage Source, V
* Output: V(2), V(3), V(4)
* Circuit Elements: B source, resistors
* Analysis:
*       A voltage source, Vinput, supplies two equal resistors, R1=R2=1k
*	The nonlinear source, B, is dependent upon the voltage at
*	node 2, V(2), by a given polynomial:
*		B = V(3) = 3 + 2*V(2) + V(2)^2
*	V(2) is established by a voltage divider with Vinput.  Additionally, 
*	V(4) is established by a voltage divider with V(3).
*	is established by a voltage divider with Vinput.  The equations
*	and expected output are as follows:
*
*	(1) V(2) = Vinput * [R2/(R1+R2)]
*	(2) V(3) = 3 + 2*V(2) + V(2)^2
*	(3) V(4) = V(3) * [R4/(R3+R4)]
*
*	Vinput, or V(1), is described on the DC line as varying from 
*	-4V to 4V in 1V steps.  Therefore, the following output, in volts, 
*	is expected:
*
*
*	VINPUT		V(2)		V(3)		V(4)
*	-4.0		-2.0		3.00		1.500
*	-3.0		-1.5		2.25		1.125
*	-2.0		-1.0		2.00		1.000
*	-1.0		-0.5		2.25		1.125
*	 0.0		 0.0		3.00		1.500
*	 1.0		 0.5		4.25		2.125
*	 2.0		 1.0		6.00		3.000
*	 3.0		 1.5		8.25		4.125
*	 4.0		 2.0		1.10		5.500
*
***********************************************************************
VINPUT 1 0 1V
B 3 0 V ={POLY(1) V(2) 3 2 1}
R1 1 2 1K
R2 2 0 1K
R3 3 4 2K
R4 4 0 2K
* Do not use singleton filtering for this small problem in parallel
.OPTIONS LINSOL TR_SINGLETON_FILTER=0
.DC VINPUT -4 4 1
.PRINT DC V(1) V(2) V(3) V(4)
*COMP V(2) zerotol=1.0e-6
.END
