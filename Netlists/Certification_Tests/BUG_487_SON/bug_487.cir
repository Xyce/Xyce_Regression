NPN Bipolar Transistor Circuit Netlist
*
* This test is patterned after the npn1.cir Gummel-Poon test.  It tests the 1D PDE
* device to insure that the 3 terminal version of the device behaves like a BJT.
* It uses the (non-default) philips mobility model, with field dependence enabled.  As such,
* this is a partial test of bug 1870(charleston):
*    "make field-dependent and carrier dependent mobilities work correctly"
*
* It also uses a realistic doping profile, based on profiles of two dopants boron and phosphorus.
*
VCC  4 0 DC 12V
RC 3 4 2K
RB 4 5 377K
* Zero volt sources used as ammeters to measure the
* base and collector currents, respectively
VMON1 5 1 0
VMON2 3 2 0

*--------------------------------------------------------------------------------
ypde npnop 2 1 0 pdebjt    
+ C0=1.0e+17
+ tecplotlevel=0 
+ gnuplotlevel=0
+ mobmodel=philips
+ fielddep=true
* relatively course mesh, so the test runs quickly
+ l=9.0e-4 nx=50
* DOPING REGIONS:
+ region={name       =            reg1,  reg2
+         function   =            file, file
+         file       =       boron.dat, phos.dat
+         nmaxchop   =         1.5e+18, 3.3e+19
+         type       =           ptype,  ntype
+         species    =              BM,  PP }
*
* ELECTRODES:
+ node={name           = collector,      base,  emitter
+       bc             = dirichlet, dirichlet,  dirichlet
+       side           =     right,    middle,  left
+       area           =    1.0e-2,    1.0e-2,  1.0e-2
+       location       =    9.0e-4,    3.0e-4,  0.0 }
*
.MODEL pdebjt ZOD  level=1
*--------------------------------------------------------------------------------
* these options force it to skip the NL poisson initial guess
.options timeint doubledcop=drift_diffusion

************************************
* **** Start Homotopy Setup ****
************************************
.options nonlin nlstrategy=0 searchmethod=0
+ maxstep=50 maxsearchstep=20 continuation=1

.options loca stepper=0 stepcontrol=0 predictor=0
+ conparam=VCC
+ initialvalue=0.0 minvalue=-1.0 maxvalue=6.0
+ initialstepsize=0.02 minstepsize=0.0001 maxstepsize=0.5 aggressiveness=1.0
+ maxsteps=5000 maxnliters=200
**********************************
* **** End Homotopy Setup ****
**********************************

.OP
* .DC VCC 0.0 6.0 0.
.PRINT DC V(4) I(VMON1) I(VMON2) V(1) V(2)

.END
