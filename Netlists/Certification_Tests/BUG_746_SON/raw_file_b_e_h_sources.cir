RAW File Output test for B, E and H Sources.
************************************************************
* This test has two purposes:
*   1) Verify that the variable type (e.g., "current") and 
*      variable names for all the B, E and H controlled 
*      sources in the header info in the .RAW file output
*
*   2) Verify that the data is actually correct for all of
*      the time-steps in the .RAW file output 
*
* The "gold" .RAW file output was generated by manually
* comparing that "gold" file with a .PRN file.  It was 
* generated for a limited set of fixed time-steps, so that
* a file comparison could be used to verify the test output's
* .RAW file against that "gold" .RAW file.
************************************************************  

* Test both the voltage (B1) and current (B11, given below) 
* formulations for the B-source
B1   1 0 V={2.0*sin(2*pi*TIME) }
R1   1 2 1K
R2   0 2 100

* Linear form of the E-Source is instantiated as
* an E-device
ELIN  3 0 1 0 1
R3    3 4 1K
R4    0 4 100

* POLY, VALUE and TABLE forms of the E-Source are
* instantiated as B-Sources
EPOLY 5 0 POLY(2) 3 0 4 0 0 .5 .5
R5    5 6 1K
R6    0 6 100

EVALUE 7 0 VALUE = 5V*SQRT(V(1,0))
R7     7 8 1K
R8     0 8 100

ETABLE  9 0 TABLE V(1,0) = (0,1) (1,2)
R9      9 10 1K
R10     0 10 100

* current form of B-Source
B11  11  0 I={2.0*sin(2*pi*TIME) }
R11  11 12 1K
R12   0 12 100

* Linear and POLY forms for the H-Source are
* instantiated as B-Sources
HLIN  13  0 B1 1000
R13   13 14 1K
R14    0 14 100

HPOLY 15  0 POLY(1) B1 0 1000
R15   15 16 1K
R16    0 16 100

.options output initial_interval=0.05
.TRAN 0 1.0

* .PRINT statement is not actually used for .RAW output.
* It was included in the netslit so that the "gold" .RAW 
* file output could manually verified against a .PRN file
* during test development
.PRINT TRAN v(1) v(10) v(11) v(12) v(13) v(14) 
+ v(15) v(16) v(2) v(3) v(4) v(5) v(6) v(7) v(8)
+ v(9) I(B1) I(ELIN) I(EPOLY) I(ETABLE) I(EVALUE)
+ I(HLIN) I(HPOLY)

.END
