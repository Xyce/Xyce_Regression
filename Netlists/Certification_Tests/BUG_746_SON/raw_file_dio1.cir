RAW File Output test for Diode Level 1.
************************************************************
* This test has two purposes:
*   1) Verify that the variable type (e.g., "current") and 
*      variable names for both diodes are correct
*
*   2) Verify that the data is actually correct for all of
*      the time-steps in the .RAW file output 
*
* The "gold" .RAW file output was generated by manually
* comparing that "gold" file with a .PRN file.  It was 
* generated for a limited set of fixed time-steps, so that
* a file comparison could be used to verify the test output's
* .RAW file against that "gold" .RAW file.
*
* The test circuit below is the Diode Clipper Circuit from
* the Users Guide.
************************************************************  

* Voltage Sources
VCC 1 0 5V
VIN 3 0 SIN(0V 10V 1kHz)

* Analysis Command
.OPTIONS OUTPUT INITIAL_INTERVAL=0.1ms
.TRAN 0 2ms

* .PRINT statement is not actually used for .RAW output.
* It was included in the netslit so that the "gold" .RAW 
* file output could manually verified against a .PRN file
* during test development
.PRINT TRAN V(1) V(2) V(3) V(4) N(D1_internal) N(D2_internal)
+ I(VCC) I(VIN)

* Diodes
D1 2 1 D1N3940
D2 0 2 D1N3940
* Resistors
R1 2 3 1K
R2 1 2 3.3K
R3 2 0 3.3K
R4 4 0 5.6K
* Capacitor
C1 2 4 0.47u
*
* GENERIC FUNCTIONAL EQUIVALENT = 1N3940
* TYPE: DIODE
* SUBTYPE: RECTIFIER
.MODEL D1N3940 D(
+ IS = 4E-10
+ RS = .105
+ N = 1.48
+ TT = 8E-7
+ CJO = 1.95E-11
+ VJ = .4
+ M = .38
+ EG = 1.36
+ XTI = -8
+ KF = 0
+ AF = 1
+ FC = .9
+ BV = 600
+ IBV = 1E-4)
*
.END
