* Test DC mode support for MAX, MIN and PP Measures
* This bug covers:
*   1) The case of one variable (vsrc1) on the .DC line, 
*      with one variable (vsrc2) in the .STEP statement.
*
*   2) the use of FROM and TO statements, where the FROM
*      value is always less than TO value. However, the DC 
*      values of the swept variable (vsrc1) are monotonically
*      decreasing.
*
* See SON Bug 884 for more details.
****************************************************************

vsrc1   1a 0 1
rload1a 1a 1b 0.1
rload1b 1b 0 1

vsrc2   2a 0 1
rload2a 2a 1b 0.2

* Reverse the step order on vsrc1 (from the test in MEASURE_DC)
* to make sure that both increasing and decreasing sequences of
* values are tested.
.DC vsrc1 5 1 -1
.STEP VSRC2 2 5 3

* Note: vsrc1:DCV0 is the syntax for printing the DC value of vsrc1,
* at it is swept. 
.print dc vsrc1:DCV0 vsrc2:DCV0 v(1a) v(2a) v(1b)

* MAX measure
.measure dc maxv1b   max v(1b)
.measure dc maxv1bFT max v(1b) FROM=2 TO=4

*MIN measure
.measure dc minv1b   min v(1b)
.measure dc minv1bFT min v(1b) FROM=2 TO=4

* PP measure
.measure dc ppv1b   pp v(1b)
.measure dc ppv1bFT pp v(1b) FROM=2 TO=4

* Tests should return -1 or -100, since the FROM-T0 window 
* does not overlap with the stepped values for VSRC1:DCV0
.measure dc maxReturnNegOne max v(1b) FROM=-4 TO=-2
.measure dc maxReturnNeg100 max v(1b) FROM=-2 TO=-4 DEFAULT_VAL=-100
.measure dc minReturnNegOne min v(1b) FROM=6 TO=8
.measure dc minReturnNeg100 min v(1b) FROM=8 TO=6 DEFAULT_VAL=-100
.measure dc ppReturnNegOne pp v(1b) FROM=-4 TO=-2
.measure dc ppReturnNeg100 pp v(1b) FROM=-4 TO=-2 DEFAULT_VAL=-100

* Signifies end of test. .sh file looks for a measure named lastMeasure.  
* This is needed for compatibility for testing with a verbose build, which 
* is often used by the Xyce developers.
.measure dc lastMeasure max v(1b) 


.end


