* Test DC mode support for ERR, ERR1 and ERR2 Measures
*
* This bug covers:
*   1) The case of one variable (vsrc1) on the .DC line,
*      with one variable (vsrc2) in the .STEP statement.
*      The swept variable (vsrc1) is descending.
*
*   2) the use of FROM and TO statements.
*
*   3) Use of a global parameter.
*
* See SON Bug 1278 for more details.
**********************************************************

.GLOBAL_PARAM P1=2.5

V3 3 0 2.5
R3 3 0 1

* Reverse the step order on vsrc1 (from the test in MEASURE_DC)
* to make sure that both increasing and decreasing sequences of
* values are tested.
.DC Vsrc1 5 -5 -1
.STEP VSRC2 2 5 3

* Note: vsrc1:DCV0 is the syntax for printing the DC value of vsrc1,
* at it is swept.
.print dc vsrc1:DCV0 vsrc2:DCV0 v(1a) v(2a) v(1b) v(3)

vsrc1   1a 0 1
rload1a 1a 1b 0.1
rload1b 1b 0 1

vsrc2   2a 0 1
rload2a 2a 1b 0.2

* ERR and ERR1 measures
.MEASURE DC ERR ERR V(1a) V(1b)
.MEASURE DC ERR1 ERR1 V(1a) V(1b)
.MEASURE DC ERR1RO ERR1 V(1b) V(1a)
.MEASURE DC ERR1MV1.5 ERR1 V(1a) V(1b) MINVAL=1.5
.MEASURE DC ERR1YMIN2.5 ERR1 V(1b) V(3) YMIN=2.5
.MEASURE DC ERR1IGNOR2.5 ERR1 V(1b) V(3) IGNOR=2.5
.MEASURE DC ERR1YMAX3.5 ERR1 V(1b) {P1} YMAX=3.5
.MEASURE DC ERR1From ERR1 PAR('V(1b)*V(1b)') V(1b) FROM=3
.MEASURE DC ERR1To ERR1 PAR('V(1b)*V(1b)') V(1b) TO=3
.MEASURE DC ERR1FT ERR1 {V(1b)*V(1b)} V(1b) FROM=0 TO=4
.MEASURE DC ERR1TF ERR1 {V(1b)*V(1b)} V(1b) FROM=4 TO=0

* ERR2 measures
.MEASURE DC ERR2 ERR2 V(1a) V(1b)
.MEASURE DC ERR2RO ERR2 V(1b) V(1a)
.MEASURE DC ERR2MV1.5 ERR2 V(1a) V(1b) MINVAL=1.5
.MEASURE DC ERR2YMIN2.5 ERR2 V(1b) V(3) YMIN=2.5
.MEASURE DC ERR2IGNORE2.5 ERR2 V(1b) V(3) IGNORE=2.5
.MEASURE DC ERR2YMAX3.5 ERR2 V(1b) {P1} YMAX=3.5
.MEASURE DC ERR2From ERR2 PAR('V(1b)*V(1b)') V(1b) FROM=3
.MEASURE DC ERR2To ERR2 PAR('V(1b)*V(1b)') V(1b) TO=3
.MEASURE DC ERR2FT ERR2 {V(1b)*V(1b)} V(1b) FROM=0 TO=4
.MEASURE DC ERR2TF ERR2 {V(1b)*V(1b)} V(1b) FROM=4 TO=0

* Tests should fail since the FROM-T0 windows do not overlap
* with the stepped values for VSRC1:DCV0
.measure dc err1Fail ERR1 v(1a) v(1b) FROM=-8 TO=-6
.measure dc err2Fail ERR2 v(1a) v(1b) FROM=8 TO=6

* Signifies end of test. .sh file looks for a measure named lastMeasure.
* This is needed for compatibility for testing with a verbose build, which
* is often used by the Xyce developers.
.measure dc lastMeasure max v(1b)

.END
