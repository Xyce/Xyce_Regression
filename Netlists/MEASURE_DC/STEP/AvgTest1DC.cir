* Test DC mode support for AVG Measures. This bug covers:
*
*   1) The case of one variable (vsrc1) on the .DC line,
*      with one variable (vsrc2) in the .STEP statement.
*
*   2) The DC values of the swept variable (vsrc1) are monotonically
*      decreasing.
*
* See SON Bug 1267 for more details.
****************************************************************

vsrc1   1a 0 1
rload1a 1a 1b 0.1
rload1b 1b 0 1

vsrc2   2a 0 1
rload2a 2a 1b 0.2

* Reverse the step order on vsrc1 (from the test in MEASURE_DC)
* to make sure that both increasing and decreasing sequences of
* values are tested.
.DC vsrc1 5 1 -1
.STEP VSRC2 2 5 3

* Note: vsrc1:DCV0 is the syntax for printing the DC value of vsrc1,
* at it is swept.
.print dc vsrc1:DCV0 vsrc2:DCV0 v(1a) v(2a) v(1b) avgv1b

.measure dc avgv1b   avg v(1b)
.meas dc avgv1bFrom  avg v(1b) FROM=3
.meas dc avgv1bTo    avg v(1b) TO=4

* these measures should get the same answer
.measure dc avgv1bFT avg v(1b) FROM=1 TO=3
.measure dc avgv1bTF avg v(1b) FROM=3 TO=1

* FROM=TO value is a failed measure, by definition
* for AVG measure.
.measure dc avgv1bFT1PT avg v(1b) FROM=4 TO=4

* Tests should fail since the FROM-T0 windows do not overlap
* with the stepped values for VSRC1:DCV0
.measure dc avgFail1 avg v(1b) FROM=-4 TO=-2
.measure dc avgFail2 avg v(1b) FROM=-2 TO=-4

* Signifies end of test. .sh file looks for a measure named lastMeasure.
* This is needed for compatibility for testing with a verbose build, which
* is often used by the Xyce developers.
.measure dc lastMeasure max v(1b)

.end
