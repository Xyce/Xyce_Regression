* Test DC mode support for AVG Measures. This bug covers:
*
*   1) The case of one variable (vsrc1) on the .DC line,
*      with one variable (vsrc2) in the .STEP statement.
*
*   2) the use of FROM and TO statements, where the FROM
*      value is always less than the TO value. However, the DC
*      values of the swept variable (vsrc1) are monotonically
*      decreasing.
*
* See SON Bug 1267 for more details.
****************************************************************

vsrc1   1a 0 1
rload1a 1a 1b 0.1
rload1b 1b 0 1

vsrc2   2a 0 1
rload2a 2a 1b 0.2

* Reverse the step order on vsrc1 (from the test in MEASURE_DC)
* to make sure that both increasing and decreasing sequences of
* values are tested.
.DC vsrc1 5 1 -1
.STEP VSRC2 2 5 3

* Note: vsrc1:DCV0 is the syntax for printing the DC value of vsrc1,
* at it is swept.
.print dc vsrc1:DCV0 vsrc2:DCV0 v(1a) v(2a) v(1b)

.measure dc avgv1b   avg v(1b)
.measure dc avgv1bFT avg v(1b) FROM=1 TO=3

* FROM=TO value is a failed measure, by definition
* for AVG measure.
.measure dc avgv1bFT1PT avg v(1b) FROM=4 TO=4

* Tests should return -1 or -100, since the FROM-T0 window
* does not overlap with the stepped values for VSRC1:DCV0
.measure dc avgReturnNegOne avg v(1b) FROM=-4 TO=-2
.measure dc avgReturnNeg100 avg v(1b) FROM=-2 TO=-4 DEFAULT_VAL=-100

* Signifies end of test. .sh file looks for a measure named lastMeasure.
* This is needed for compatibility for testing with a verbose build, which
* is often used by the Xyce developers.
.measure dc lastMeasure max v(1b)

.end
