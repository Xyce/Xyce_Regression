* Test DC mode support for ERROR Measure
*
* This bug covers the case of one variable in the .DC
* line without a .STEP statement.  It also tests that
* INDEPVARCOL qualifer is safely ignored for DC mode.
*
* See SON Bug 888 for more details.
********************************************************

* Note: the comparison file (RawData) was made was rload1a=0.1.
* The simulation is run with rload1a=0.12. 
vsrc1   1a 0 1
*rload1a 1a 1b 0.1
rload1a 1a 1b 0.12
rload1b 1b 0 1

vsrc2   2a 0 1
rload2a 2a 1b 0.2

* step vsrc1 in "descending order"
.DC vsrc1 5 1 -1 

* Note: vsrc1:DCV0 is the syntax for printing the DC value of vsrc1,
.print dc vsrc1:DCV0 RLOAD1A:R v(1a) v(2a) v(1b) 

* Test with FILE= in .PRN format.  Use all three norms.
* Since the INDEPVARCOL is ignored for a DC mode ERROR measure, also test that an
* invalid value for it does not cause an error.
.measure dc FitErrorInfPrn error v(1b) file=ErrorTest1DCRawData.prn comp_function=infnorm indepvarcol=5 depvarcol=5
.measure dc FitErrorL1Prn error v(1b) file=ErrorTest1DCRawData.prn comp_function=l1norm indepvarcol=20 depvarcol=5
.measure dc FitErrorL2Prn error v(1b) file=ErrorTest1DCRawData.prn comp_function=l2norm indepvarcol=-1 depvarcol=5

*Test with FILE= in .CSD format.
.measure dc FitErrorInfcsd error v(1b) file=ErrorTest1DCRawData.csd comp_function=INFNORM depvarcol=5
.measure dc FitErrorL1csd error v(1b) file=ErrorTest1DCRawData.csd comp_function=L1NORM depvarcol=5
.measure dc FitErrorL2csd error v(1b) file=ErrorTest1DCRawData.csd comp_function=L2NORM depvarcol=5

*Test with FILE= in .CSV format.
.measure dc FitErrorInfcsv error v(1b) file=ErrorTest1DCRawData.csv comp_function=INFNORM depvarcol=4
.measure dc FitErrorL1csv error v(1b) file=ErrorTest1DCRawData.csv comp_function=L1NORM depvarcol=4
.measure dc FitErrorL2csv error v(1b) file=ErrorTest1DCRawData.csv comp_function=L2NORM depvarcol=4

* Test with FILE= in Comma-Delimited .PRN format
.measure dc FitErrorInfCDPrn error v(1b) file=ErrorTest1DCCommaDelimRawData.prn comp_function=infnorm depvarcol=5
.measure dc FitErrorL1CDPrn error v(1b) file=ErrorTest1DCCommaDelimRawData.prn comp_function=l1norm depvarcol=5
.measure dc FitErrorL2CDPrn error v(1b) file=ErrorTest1DCCommaDelimRawData.prn comp_function=l2norm depvarcol=5

.end


