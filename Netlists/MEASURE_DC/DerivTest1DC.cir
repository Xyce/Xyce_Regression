* Test DC mode support for DERIV Measures
*
* This bug covers:
*   1) the case of one variable in the .DC line,
*      without a .STEP statement.
*
* See SON Bug 1282 for more details.
********************************************************

V1 1 0 1
R1 1 0 1

E2 2 0 VALUE={V(1)*V(1)}
R2 2 0 3

E3 3 0 VALUE={(V(1)-3)*(V(1)-3.1)}
R3 3 0 3

V4 4 0 1
R4 4 0 1

.DC V1 1 5 1
.PRINT DC V(1) V(2) V(3) I(R2) V(4)

* DERIV
.MEASURE DC DERIVV2AT2.5 DERIV V(2) AT=2.5

* test AT value at start of sweep. The WHEN and
* WHENFT measures will fail since v(4) is constant.
.MEASURE DC DERIVV2AT1 DERIVATIVE V(2) AT=1
.MEASURE DC ATFT DERIV v(2) AT=1  FROM=1 TO=1
.MEASURE DC WHEN DERIV v(2) WHEN v(4)=1  
.MEASURE DC WHENFT DERIV v(2) WHEN v(4)=1  FROM=1 TO=1

* Various constant signals.  The CONSTANT-WHEN2 measure will
* fail because VM(4) is a constant.
.MEASURE DC CONSTANT-AT DERIV V(4) AT=2.5
.MEASURE DC CONSTANT-WHEN1 DERIV V(4) WHEN V(2)=10
.MEASURE DC CONSTANT-WHEN2 DERIV V(2) WHEN V(4)=1

* WHEN syntaxes
.MEASURE DC DERIVV2WHEN10 deriv V(2) WHEN V(2)=10

* Expressions in WHEN clause
.MEASURE DC derivExp1 DERIV V(2) when v(2)={4*v(1)}
.MEASURE DC derivExp2 DERIV V(2) when {v(2)+1}=6
.MEASURE DC derivExp3 DERIV V(2) when v(2)={3+2}
.MEASURE DC derivExp4 DERIV V(2) when v(2)={v(1)+6}

* Expression in DERIV clause, and derivative is negative
.MEASURE DC derivExp5 DERIV {V(1)-V(2)} when v(2)=10

* FROM and TO
* Test that the interpolated WHEN time is within the measure window.
* The FROM1 and FROM3 measures should find the second time v(3)=0.5
* The FROM2 and FROM4 measures should find the first time.
.MEASURE DC FROM1 DERIV V(3) WHEN V(3)=0.5 FROM=3
.MEASURE DC FROM2 DERIV V(3) WHEN V(3)=0.5 FROM=2.25
.MEASURE DC FROM3 DERIV V(3) WHEN V(3)=0.5 FROM=2.75
.MEASURE DC FROM4 DERIV V(3) WHEN V(3)=0.5 FROM=2.25 TO=2.75

* This should get the same value as the from4 measure
.MEASURE DC FROM5 DERIV V(3) WHEN V(3)=0.5 TO=2.25 FROM=2.75

* The TO2 measure should fail, since the TO1 measure succeeds at V(1)=3.4
.MEASURE DC TO1 DERIV V(2) WHEN V(2)=12 TO=3.75
.MEASURE DC TO2 DERIV V(2) WHEN V(2)=12 TO=3.25

* lead current
.MEASURE DC derivir2at2.5 deriv I(R2) AT=2.5

* measures should fail
.measure dc FAIL1 DERIV v(2) WHEN v(3)=10
.measure dc FAIL2 DERIV v(2) AT=10

* Test case of both FROM and TO given, but both are outside of the
* measurement window
.measure dc FAIL3 DERIV v(2) WHEN v(2)=2 FROM=6 TO=10
.measure dc FAIL4 DERIV v(2) WHEN v(2)=2 FROM=-5 TO=0

* Tests should fail
.measure dc derivFail1 deriv v(1) AT=10
.measure dc derivFail2 deriv v(1) when v(1)=10

* FROM and TO qualifiers take precedence over AT.
* So, these are failed measures.
.MEASURE DC atFailFrom DERIV V(2) AT=1 FROM=3 TO=4
.MEASURE DC atFailTo DERIV V(2) AT=2 TO=1

.end
