*********************************************************************
* Test various bad measure lines for the ERROR measure for DC mode
* measures.  This is done since the ERROR measure has a different 
* syntax then the other measure types. The tests are:
*
*   a) requesting a non-existent column in the comparison
*      file (FILE=).
*
*   b) missing or negative values for the DEPVARCOL column values.
*
* This test only covers the cases that are specific to DC mode, 
* since the DC-mode ERROR measure only requires the DEPVARCOL 
* qualifier.  The similar test for TRAN mode ERROR measures covers 
* more error conditions since those measures also require the 
* INDEPVARCOL qualifier.  The test for TRAN mode also covers 
* error conditions that are common to all three measure modes,
* TRAN, AC and DC.
*
*********************************************************************

vsrc1   1a 0 1
rload1a 1a 1b 0.12
rload1b 1b 0 1

vsrc2   2a 0 1
rload2a 2a 1b 0.2

* step vsrc1 in "descending order"
.DC vsrc1 5 1 -1 

* Note: vsrc1:DCV0 is the syntax for printing the DC value of vsrc1,
.print dc vsrc1:DCV0 RLOAD1A:R v(1a) v(2a) v(1b) 

* column 10 does not exist in the file ErrorTest1DCRawData.prn
.measure dc DepVarColInvalid error v(1) FILE=ErrorTest1DCRawData.prn 
+ comp_function=l2norm depvarcol=10

* negative or missing column value for depvarcol should produce an error
.measure dc DepVarColMissing error v(1) FILE=ErrorTest1DCRawData.prn 
+ comp_function=l2norm
.measure dc DepVarColNegative error v(2) FILE=ErrorTest1DCRawData.prn 
+ comp_function=l2norm depvarcol=-1

.end


