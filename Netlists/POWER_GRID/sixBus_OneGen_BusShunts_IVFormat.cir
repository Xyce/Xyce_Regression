* Test of PowerGridBusShunt Device in 6-bus network, in I=YV format
* It also tests that the default values (of 0) for G and B work for 
* the Shunt device.

******************************************************
* Have bus 1 be the slack bus.  Ammeter is wired     *
* so that current flows into bus 1                   *
******************************************************
V1R bus1R ammBus1R 1V
V1I bus1I ammBus1I 0.0V
Vamm1R 0 ammBus1R 0V
Vamm1I 0 ammBus1I 0V

***********************
* Branch definitions  *
***********************
YPowerGridBranch pg12 bus1R bus2R bus1I bus2I AT=IV R=0.1 X=0.2 B=0.02 
YPowerGridBranch pg14 bus1R bus4R bus1I bus4I AT=IV R=0.05 X=0.2 B=0.02
YPowerGridBranch pg15 bus1R bus5R bus1I bus5I AT=IV R=0.08 X=0.3 B=0.03
YPowerGridBranch pg23 bus2R bus3R bus2I bus3I AT=IV R=0.05 X=0.25 B=0.03 
YPowerGridBranch pg24 bus2R bus4R bus2I bus4I AT=IV R=0.05 X=0.1 B=0.01 
YPowerGridBranch pg25 bus2R bus5R bus2I bus5I AT=IV R=0.1 X=0.3 B=0.02 
YPowerGridBranch pg26 bus2R bus6R bus2I bus6I AT=IV R=0.07 X=0.2 B=0.025
YPowerGridBranch pg35 bus3R bus5R bus3I bus5I AT=IV R=0.12 X=0.26 B=0.025 
YPowerGridBranch pg36 bus3R bus6R bus3I bus6I AT=IV R=0.02 X=0.1 B=0.01
YPowerGridBranch pg45 bus4R bus5R bus4I bus5I AT=IV R=0.2 X=0.4 B=0.04 
YPowerGridBranch pg56 bus5R bus6R bus5I bus6I AT=IV R=0.1 X=0.3 B=0.03 

**************************
* Bus Shunt definitions  *
**************************
YPowerGridBusShunt pg1GND bus1R 0 bus1I 0 AT=iv g=0 b=0.1 
YPowerGridBusShunt pg2GND bus2R 0 bus2I 0 AT=IV G=0.2 B=0 
YPowerGridBusShunt pg3GND bus3R 0 bus3I 0 AT=IV G=0.2 B=0.1 
YPowerGridBusShunt pg4GND bus4R 0 bus4I 0 AT=IV G=0.2 B=0.1 
YPowerGridBusShunt pg5GND bus5R 0 bus5I 0 AT=IV G=0.2 B=0.1 
YPowerGridBusShunt pg6GND bus6R 0 bus6I 0 AT=IV G=0.2 B=0.1 

*****************************
* No PV Bus definitions yet *
*****************************

***********************
* load definitions    *
***********************
XLoad2 bus2R bus2I CPL PARAMS: P=0.2 Q=0.2
XLoad3 bus3R bus3I CPL PARAMS: P=0.2 Q=0.2
XLoad4 bus4R bus4I CPL PARAMS: P=0.2 Q=0.2
Xload5 bus5R bus5I CPL PARAMS: P=0.2 Q=0.2
Xload6 bus6R bus6I CPL PARAMS: P=0.2 Q=0.2

*****************************************************
* simulation control statement and output statement *
*****************************************************
.DC V1R 1 1 1
.PRINT DC width=10 precision=6 V(bus1R) V(bus1I) V(bus2R) V(bus2I) 
+ V(bus3R) V(bus3I) V(bus4R) V(bus4I) V(bus5R) V(bus5I) V(bus6R) V(bus6I)
+ {V(bus1R)*I(Vamm1R)+V(bus1I)*I(Vamm1I)}
+ {V(bus1I)*I(Vamm1R)-V(bus1R)*I(Vamm1I)}
+ {V(bus2R)*I(Xload2:VammR)+V(bus2I)*I(Xload2:VammI)}
+ {V(bus2I)*I(Xload2:VammR)-V(bus2R)*I(Xload2:VammI)}
+ {V(bus3R)*I(Xload3:VammR)+V(bus3I)*I(Xload3:VammI)}
+ {V(bus3I)*I(Xload3:VammR)-V(bus3R)*I(Xload3:VammI)}
+ {V(bus4R)*I(Xload4:VammR)+V(bus4I)*I(Xload4:VammI)}
+ {V(bus4I)*I(Xload4:VammR)-V(bus4R)*I(Xload4:VammI)}
+ {V(bus5R)*I(Xload5:VammR)+V(bus5I)*I(Xload5:VammI)}
+ {V(bus5I)*I(Xload5:VammR)-V(bus5R)*I(Xload5:VammI)}
+ {V(bus6R)*I(Xload6:VammR)+V(bus6I)*I(Xload6:VammI)}
+ {V(bus6I)*I(Xload6:VammR)-V(bus6R)*I(Xload6:VammI)}

*************************************************
* Subcircuit defintion of a constant power load *
*************************************************
.SUBCKT CPL RNode INode PARAMS: P=0.5 Q=0.0 CurrLim=1000

* Ammeter at load is defined so that power flows into the load from the bus
VammR ammR 0 0V
VammI ammI 0 0V
BloadR RNode ammR I={limit( (P*V(RNode)+Q*V(INode))/(V(RNode)*V(RNode)+V(INode)*V(INode)),
+ -CurrLim,CurrLim)}
BloadI INode ammI  I={limit( (P*V(INode)-Q*V(RNode))/(V(RNode)*V(RNode)+V(INode)*V(INode)),
+ -CurrLim,CurrLim)}

.ENDS
 
.end
