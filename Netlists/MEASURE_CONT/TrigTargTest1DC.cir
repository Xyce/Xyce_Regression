* Test DC_CONT mode support for TRIG-TARG Measures
*
* This bug covers:
*   1) the case of one variable in the .DC line,
*      without a .STEP statement. where the swept
*      variable is increasing.
*
*   2) An ascending sweep variable.
*****************************************************

* For testing convenience send the output for the AC_CONT
* measures to the <netlistName>.ma0 file.
.OPTIONS MEASURE USE_CONT_FILES=0

.DC V1 1 10 1
.PRINT DC V(1) V(2) V(3)

V1 1 0 1
R1 1 0 1

B2 2 0 V={(V(1)-2.5)*(V(1)-2.5)*(V(1)-7.5)*(V(1)-7.5)/4}
R2 2 3 1
R3 3 0 1

* AT tests
.MEASURE DC_CONT TRIGTARGAT TRIG AT=2.5 TARG AT=7.5
.MEASURE DC_CONT TRIGTARGAT1 TRIG AT=2.5 TARG V(2)=5 CROSS=1
.MEASURE DC_CONT TRIGTARGAT2 TRIG V(2)=5 CROSS=1 TARG AT=7.5

* Basic tests
.MEASURE DC_CONT TRIGTARG1 TRIG V(2)=3 CROSS=1 TARG V(2)=7 CROSS=1 
.MEASURE DC_CONT TRIGTARG2 TRIG V(2)=3 CROSS=1 TARG V(2)=7 CROSS=2
.MEASURE DC_CONT TRIGTARG3 TRIG V(2)=3 CROSS=2 TARG V(2)=7 CROSS=1

* Repeat core tests with RISE and FALL.
.MEASURE DC_CONT RISEFALL1 TRIG V(2)=3 RISE=1 TARG V(2)=7 FALL=2 
.MEASURE DC_CONT RISEFALL2 TRIG V(2)=3 RISE=2 TARG V(2)=7 FALL=1

* Add TD for both TRIG and TARG.  Note that the TRIG TD value will also be used
* for the TARG TD value, if only the TRIG TD value is given.  An early TD value
* is essentially ignored.
.MEASURE DC_CONT TRIGTARG7 TRIG V(2)=3 CROSS=1 TD=2 TARG V(2)=7 CROSS=1 
.MEASURE DC_CONT TRIGTARG8 TRIG V(2)=3 CROSS=1 TD=2 TARG V(2)=7 CROSS=1 TD=5
.MEASURE DC_CONT TRIGTARG9 TRIG V(2)=3 CROSS=1 TARG V(2)=7 CROSS=1 TD=5
.MEASURE DC_CONT TDearly TRIG V(2)=3 CROSS=1 TD=0.5 TARG V(2)=7 CROSS=1

* failed measures
.MEASURE DC_CONT ATEarly TRIG AT=0 TARG AT=-1
.MEASURE DC_CONT ATLate TRIG AT=11 TARG AT=12
.MEASURE DC_CONT TrigTDLate TRIG v(2)=3 CROSS=1 TD=15 TARG v(2)=7 CROSS=1 TD=2
.MEASURE DC_CONT TargTDLate TRIG v(2)=3 CROSS=1 TARG v(2)=7 CROSS=2 TD=12
.MEASURE DC_CONT ValNotFound TRIG v(2)=200 CROSS=1 TARG v(2)=-200 CROSS=2

.END
