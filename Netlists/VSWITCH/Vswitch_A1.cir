Test of the Voltage Switch Implementation of a NOR Gate
*###############################################################################
* Tier No.: 1   
* Directory/Circuit Name: VSWITCH/VSWITCH.cir                                        
* Description:  Test of  voltage controlled switch model.  The switch   
* behaves like a resistor that toggles between a very small on 
* resistance, and a very large off resistance which depends 
*  upon the controlling voltage.
* Input:  VCC, V3, V4
* Output: V(1), V(3), V(4)
* Analysis:
*    The circuit equivalent to the MOSFET implementation of a
*    NOR gate with the transistors replaced by two switches, S1 and S2,  and
*    a load resistance connected to 5V, VCC or V(2).  The controlling voltages
*    at nodes 3 and 4 represent the two inputs to the NOR gate,
*    which has the following logic table:
*      InputA  InputB  Output
*      ______________________________
*    
*      0(Low)  0(Low)  1(High)
*      0(Low)  1(High) 0(Low)
*      1(High) 0(Low)  0(Low)
*      1(High) 1(High) 0(Low)
*    The two input signals, V(3) and V(4),  are a sequence of 
*    logic 0 and 1 described by two PWL voltage sources.  A transient 
*    analysis of the circuit from 0 to 4us circuit from 0us to 4us 
*    yields the following expected output:
*        0us  1us  2us  3us  4us
*
*  V(3)=InputA    0  5  0  5  5
*  V(4)=InputB    0  0  5  5  5
*  V(1)=Output    4.99  .005  .005  .025  .025
*Note: V(1)=0.005 and V(1)=0.025 are logic 0.
*###############################################################################
RL 2 1 1K
S1 1 0 3 0 SW
S2 1 0 4 0 SW
VCC 2 0 5
V3 3 0 PWL(0 0 1U 0 1.01U 5 2U 5 2.01U 0 3U 0 3.01U 5)
R3 3 0 1
V4 4 0 PWL (0 0 2U 0 2.01U 5)
R4 4 0 1
.MODEL SW VSWITCH(RON=1 ROFF=1MEG VON=1 VOFF=0)
.TRAN 0.02U 4U
.PRINT TRAN V(1) V(3) V(4)
.END

